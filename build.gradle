group = 'at.jku.ssw'
version = '1.0-SNAPSHOT'

/* PLUGINS ================================================================= */

apply plugin: 'java'
apply plugin: 'application'
sourceCompatibility = 10
targetCompatibility = 10

mainClassName = 'at.jku.ssw.java.bytecode.reducer.JReduce'

/* PROPERTIES ============================================================== */

ext {
    asmVersion = '6.2.1'
    javassistVersion = '3.23.1-GA'

    junitVersion = '5.3.1'
    hamcrestVersion = '1.3'

    apacheLoggingVersion = '2.11.1'
    apacheCommonsVersion = '1.4'
}

/* CUSTOM TASKS ============================================================ */

/**
 * Generate Javadoc from the source files.
 */
javadoc {
    source = sourceSets.main.allJava
}

/**
 * Generate JAR file including dependencies.
 */
jar {
    manifest {
        attributes 'Implementation-Title': 'Java Bytecode Reducer',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    baseName = 'jreduce'
    archiveName = "${jar.baseName}.${jar.extension}"
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

/**
 * Run all tests.
 */
test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

/**
 * Run the application.
 */
run {
    classpath = sourceSets.main.runtimeClasspath
}

/**
 * Generate the wrapper.
 */
wrapper {
    gradleVersion = '4.10'
}

/* REPOSITORIES ============================================================ */

repositories {
    mavenCentral()
}

/* DEPENDENCIES ============================================================ */

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: apacheLoggingVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: apacheLoggingVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-iostreams', version: apacheLoggingVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion

    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrestVersion


    compile group: 'org.javassist', name: 'javassist', version: javassistVersion
    compile group: 'org.ow2.asm', name: 'asm', version: asmVersion
    compile group: 'org.ow2.asm', name: 'asm-tree', version: asmVersion
    compile group: 'org.ow2.asm', name: 'asm-commons', version: asmVersion

    compile group: 'commons-cli', name: 'commons-cli', version: apacheCommonsVersion
}
