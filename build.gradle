group = 'at.jku.ssw'
version = '1.0-SNAPSHOT'

/* PLUGINS ================================================================= */

apply plugin: 'java'
sourceCompatibility = 10
targetCompatibility = 10

/* CUSTOM TASKS ============================================================ */

/**
 * Generate Javadoc from the source files.
 */
javadoc {
    source = sourceSets.main.allJava
}

/**
 * Generate a JAR including dependencies
 */
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Java Bytecode Reducer',
                'Implementation-Version': version,
                'Main-Class': 'at.jku.ssw.java.bytecode.reducer.JReduce'
    }
    baseName = 'jreduce'
    archiveName = "${baseName}.${jar.extension}"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

wrapper {
    gradleVersion = '4.9'
}

/* REPOSITORIES ============================================================ */

repositories {
    mavenCentral()
}

/* DEPENDENCIES ============================================================ */

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.11.0'
    compile 'org.apache.logging.log4j:log4j-core:2.11.0'
    compile 'org.apache.logging.log4j:log4j-iostreams:2.11.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

    compile 'org.javassist:javassist:3.22.0-GA'
    compile 'org.ow2.asm:asm:6.2'

    compile 'commons-cli:commons-cli:1.4'
}
